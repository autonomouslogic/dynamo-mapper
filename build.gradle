plugins {
    id "java"
    id "idea"
    id "org.danilopianini.git-sensitive-semantic-versioning-gradle-plugin" version "0.3.11"
    id "io.freefair.lombok" version "6.4.3"
}

group "com.autonomouslogic.dynamomapper"

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

repositories {
    mavenCentral()
}

dependencies {
    implementation "software.amazon.awssdk:dynamodb:2.17.190"
    implementation "com.fasterxml.jackson.core:jackson-databind:2.13.2.2"

    testImplementation "org.junit.jupiter:junit-jupiter-api:5.8.2"
    testImplementation "org.junit.jupiter:junit-jupiter-params:5.8.2"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.8.2"

    testImplementation "commons-codec:commons-codec:1.15"
    testImplementation "software.amazon.awssdk:dynamodb-enhanced:2.17.189"
    testImplementation "com.amazonaws:aws-java-sdk-dynamodb:1.12.218"
    testImplementation "org.apache.commons:commons-math3:3.6.1"
}

sourceSets {
    integrationTest {
        java.srcDir "src/integrationTest/java"
        resources.srcDir "src/integrationTest/resources"
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
    }
}

configurations {
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}

test {
    useJUnitPlatform()

    testLogging {
        events = [ "FAILED", "PASSED", "SKIPPED" ]
        showExceptions = true
        exceptionFormat = "FULL"
        showExceptions = true
        showCauses = true
        showStackTraces = true
    }
}

task integrationTest(type: Test, description: "Runs the integration tests", group: "verification") {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath

    useJUnitPlatform()

    testLogging {
        events = [ "FAILED", "PASSED", "SKIPPED" ]
        showExceptions = true
        exceptionFormat = "FULL"
        showExceptions = true
        showCauses = true
        showStackTraces = true
    }
}
